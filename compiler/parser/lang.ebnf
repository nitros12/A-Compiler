@@grammar :: Wew

@@comments :: /{~(\n|.)*~}/
@@eol_comments :: /~~.*?$/

@@left_recursion :: False

@@parseinfo :: True

start = {decl}+ $ ;

base_type
  =
  t: ('u1'
     | 'u2'
     | 'u4'
     | 'u8'
     | 's1'
     | 's2'
     | 's4'
     | 's8')
  ;

ptr_type = "*" t:type ;

const_type = "|" t:type "|" ;

array_type = "[" t:type ["@" s:int] "]"  ;

fun_type = "(" t:','.{type} ')' '->' r:type ;

type
  =
  | base_type
  | ptr_type
  | array_type
  | const_type
  | fun_type
  | "(" type ")"
  ;

statement
  =
  | ( var_decl
    | scope
    | if_stmt
    | loop_stmt
    | return_stmt
    ) [';']  # semicolon is optional for non expr-stmts
  | expr ';'
  ;

scope
  = '{' body:{statement}* '}'
  ;

if_stmt
  = "if" ~ e:expr t:scope ['else' f:scope]
  ;

loop_stmt
  =
  "while" ~ e:expr t:scope
  ;

return_stmt = "return" ~ e:expr ;

expr
  =
  | assign_expr
  | logical
  ;

fun_decl
  = "fn" ~ name:name '(' params:','.{name ':' type} ')' '->' r:type >scope
  ;

optional_def
  =
  | ":=" ~ val:expr typ:`infer`  # inferred
  | ":" typ:type ["=" val:expr]  # static
  ;

var_decl
  = "var" ~ name:name >optional_def
  ;

decl
  =
  ( fun_decl
  | var_decl
  ) [';']
  ;

assign_expr
  = left:logical op:'=' right:expr
  ;

logical
  =
  | bitwise
  | boolean
  | comparison
  ;

bitwise
  =
  left:comparison op:('|' |
                      '^' |
                      '&' ) right:logical
  ;

boolean
  =
  left:comparison op:('||' | '&&') ~ right:logical
  ;

comparison
  =
  | equality
  | relation
  | shift
  ;

equality
  =
  left:shift op:('!=' |
                 '==' ) ~ right:comparison
  ;

relation
  =
  left:shift op:('<=' |
                 '>=' |
                 '<'  |
                 '>'  ) ~ right:comparison
  ;

shift
  =
  | bitshift
  | binop
  ;

bitshift
  =
  left:binop op:('>>' | '<<') ~ right:shift
  ;

binop
  =
  | additive
  | multiplicative
  ;

additive
  =
  left:multiplicative op:('+' | '-') ~ right:binop
  ;

multiplicative
  =
  | multiply
  | unop
  ;

multiply
  =
  left:unop op:('*' | '/') right:multiplicative
  ;

unop
  =
  | prefix
  | postop
  ;

prefix
  =
  op:('*'  |
      '--' |
      '++' |
      '~'  |
      '!'  |
      '-'  |
      '&'  |
      '+'  ) right:unop
  ;

postop
  =
  | postfix
  | singular
  ;

postfixexpr
  =
  | '(' args:','.{expr} ')' type:`f`
  | '[' ~ args:expr ']' type:`b`
  | op:'++' type:`d`
  | op:'--' type:`d`
  | op:':::' ~ t:type type:`c`  # reinterpret expr
  | op:'::' ~ t:type type:`c`  # cast expr
  ; 

postfix
  =
  left:singular exprs:{postfixexpr}
  ;

singular
  =
  | literal
  | identifier
  | subexpr
  ;

subexpr
  =
  '(' @:expr ')'
  ;

literal
 =
 | int_lit
 | str
 | chr
 | arr_lit
 ;

# array literals will be pointers if in an expression, otherwise expand into index setting if on rhs of an initialisation
arr_lit
  =
  '{' obj:','.{expr} '}'
  ;

int = /-?\d+/ ;
int_lit = val:(>int) ['/' type:base_type] ;
str = str:/".+"/ ;
chr = chr:/'.'/ ;
name = /[A-Za-z]\w*/ ;
identifier = identifier:name ;
