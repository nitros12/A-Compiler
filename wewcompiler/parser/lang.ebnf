@@grammar :: Wew
@@comments :: /{~(\n|.)*~}/
@@eol_comments :: /\/\/.*?$/
@@left_recursion :: False
@@parseinfo :: True

start = {decl}+ $ ;


base_type = t:('u1' | 'u2' | 'u4' | 'u8' | 's1' | 's2' | 's4' | 's8') ;


ptr_type = '*' t:type ;


void_type = t:'()' ;


const_type = '|' t:type '|' ;


array_type = '[' t:type ['@' s:int] ']' ;


fun_type = '(' t:','.{type} ')' '->' r:type ;


type = base_type | ptr_type | array_type | const_type | fun_type | void_type | '(' type ')' ;


statement = (var_decl | scope | if_stmt | loop_stmt | return_stmt) [';'] | expr ';' ;


scope = '{' body:{statement} '}' ;


elif = 'elif' e:expr t:scope ;
if_stmt = 'if' ~ e:expr t:scope elf:{elif} ['else' f:scope] ;


loop_stmt = 'while' ~ e:expr t:scope ;


return_stmt = 'return' ~ e:expr ;


fun_decl = 'fn' ~ name:name '(' params:','.{name ':' type} ')' ['->' r:type] >scope ;


optional_def = ':=' ~ val:expr typ:`'infer'` | ':' typ:type ['=' val:expr] ;


var_decl = 'var' ~ name:name >optional_def ;


decl = (fun_decl | var_decl) [';'] ;

expr = assign_expr | boolean_pre ;

assign_expr = left:boolean_pre op:'=' right:expr ;

boolean_pre = boolean | bitwise_pre ;
boolean = left:bitwise_pre op:('or' | 'and') ~ right:boolean_pre ;


bitwise_pre = bitwise | equality_pre ;
bitwise = left:equality_pre rest:{bitwise_rep}+ ;
bitwise_rep = op:('|' | '^' | '&') right:equality_pre ;


equality_pre = equality | relation_pre ;
equality = left:relation_pre rest:{equality_rep}+ ;
equality_rep = op:('!=' | '==') right:relation_pre ;


relation_pre = relation | bitshift_pre ;
relation = left:bitshift_pre rest:{relation_rep}+ ;
relation_rep = op:('<=' | '>=' | '<' | '>') right:bitshift_pre ;


bitshift_pre = bitshift | additive_pre ;
bitshift = left:additive_pre rest:{bitshift_rep}+ ;
bitshift_rep = op:('>>' | '<<') right:additive_pre ;


additive_pre = additive | multiply_pre ;
additive = left:multiply_pre rest:{additive_rep}+ ;
additive_rep = op:('+' | '-') right:multiply_pre ;


multiply_pre = multiply | unop_pre ;
multiply = left:unop_pre rest:{multiply_rep}+ ;
multiply_rep = op:('*' | '/') right:unop_pre ;


unop_pre = prefix | postop ;

prefix = op:('*' | '--' | '++' | '~' | '!' | '-' | '&' | '+') right:unop_pre ;


postop = postfix | singular ;


postfixexpr = | '(' args:','.{expr} ')' type:`'f'`
    | '[' ~ args:expr ']' type:`'b'`
    | op:'++' type:`'d'`
    | op:'--' type:`'d'`
    | op:':::' ~ t:type type:`'c'`
    | op:'::' ~ t:type type:`'c'` ;


postfix = left:singular exprs:{postfixexpr} ;


singular = literal | identifier | subexpr ;


subexpr = '(' @:expr ')' ;


literal = int_lit | str | chr | arr_lit ;


arr_lit = '{' obj:','.{expr} '}' ;


int = /-?\d+/ ;


int_lit = val:(>int) ['/' type:base_type] ;


str = str:/"[^"]+"/ ;


chr = chr:/'.'/ ;


name = /[A-Za-z]\w*/ ;


identifier = identifier:name ;
